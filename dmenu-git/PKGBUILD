# Maintainer: Edward Hernandez <ehernandez@email.wm.edu>
# AUR maintainer:  HÃ¥vard Pettersson <mail@haavard.me>

_pkgname=dmenu
pkgname=$_pkgname-git
pkgver=4.6.4.gb3d9451
pkgrel=1
pkgdesc="A generic menu for X"
url="http://tools.suckless.org/dmenu/"
arch=('i686' 'x86_64')
license=('MIT')
depends=('sh' 'libxinerama' 'libxft')
makedepends=('git')
provides=($_pkgname)
conflicts=($_pkgname)
source=(git://git.suckless.org/$_pkgname)
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags --long | sed 's/-/./g'
}

prepare() {
  cd $_pkgname
  # to use a custom config.h, place it in the package directory
  if [[ -f ${SRCDEST}/config.h ]]; then
      cp "${SRCDEST}/config.h" .
  fi
  sed -e 's/monospace/Liberation Mono/' \
      -e '/normbgcolor/s/#[0-9a-fA-F]*/#3a3a3a/' \
      -e '/normfgcolor/s/#[0-9a-fA-F]*/#d0d0d0/' \
	  -e '/selbgcolor/s/#[0-9a-fA-F]*/#3a3a3a/' \
	  -e '/selfgcolor/s/#[0-9a-fA-F]*/#d75f87/' \
	  -i config.def.h
}

build(){
  cd $_pkgname
  make \
    X11INC=/usr/include/X11 \
    X11LIB=/usr/lib/X11
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
