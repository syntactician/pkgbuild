# Maintainer: Edward Hernandez <ehernandez at email dot wm dot edu>
# AUR maintainer: mar77i <mysatyre at gmail dot com>

pkgname=st-git
_pkgname=st
pkgver=20160221.4fdba86
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://git.suckless.org/st/'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
source=('git://git.suckless.org/st')
sha1sums=('SKIP')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	local file
	cd "${srcdir}/${_pkgname}"
	sed \
		-e '/char font/s/pixelsize=[1-9]*:/pixelsize=15:/' \
		-e '/borderpx/s/= .*/= 20;/' \
		-e '/char worddelimiters/s/= .*/= " '"'"'`\\\"()[]{}<>|";/' \
		-e '/int defaultcs/s/= .*/= 1;/' \
		-e 's/"black"/"#2b303b"/' \
		-e 's/"red3"/"#bf616a"/' \
		-e 's/"green3"/"#a3be8c"/' \
		-e 's/"yellow3"/"#ebcb8b"/'\
		-e 's/"blue2"/"#8fa1b3"/' \
		-e 's/"magenta3"/"#b48ead"/' \
		-e 's/"cyan3"/"#96b5b4"/' \
		-e 's/"gray90"/"#c0c5ce"/' \
		-e 's/"gray50"/"#65737e"/' \
		-e 's/"red"/"#d08770"/' \
		-e 's/"green"/"#343d46"/' \
		-e 's/"yellow"/"#4f5b66"/' \
		-e 's/"#5c5cff"/"#a7adba"/' \
		-e 's/"magenta"/"#dfe1e8"/' \
		-e 's/"cyan"/"#ab7967"/' \
		-e 's/"white"/"#eff1f5"/' \
		-i config.def.h
	sed \
		-e 's/CPPFLAGS =/CPPFLAGS +=/g' \
		-e 's/CFLAGS =/CFLAGS +=/g' \
		-e 's/LDFLAGS =/LDFLAGS +=/g' \
		-e 's/_BSD_SOURCE/_DEFAULT_SOURCE/' \
		-i config.mk
	sed '/@tic/d' -i Makefile
	for file in "${source[@]}"; do
		if [[ "$file" == "config.h" ]]; then
			# add config.h if present in source array
			# Note: this supersedes the above sed to config.def.h
			cp "$srcdir/$file" .
			continue
		elif [[ "$file" == *.diff ]]; then
			# add all patches present in source array
			patch -Np1 <"$srcdir/$file"
		fi
	done
}

build() {
	cd "${srcdir}/${_pkgname}"
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/${_pkgname}"
	make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
